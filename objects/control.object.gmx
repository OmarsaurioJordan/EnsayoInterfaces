<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>&lt;undefined&gt;</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>0</depth>
  <persistent>0</persistent>
  <parentName>&lt;undefined&gt;</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>randomize();
depth = -room_height;
alarm[0] = 1;
alarm[2] = 1;
alarm[4] = 1;
instance_create(room_width * 0.5, room_height * 0.5, sombraedificios);
instance_create(0, 0, sombrageneral);
audio_channel_num(150);

// variables del juego
nombre = ""; // id que reconoce al usuario en la web
maxciclo = m_segundo * 15; // microsegundos que dura el ciclo de juego
monedas = 0; // piezas de valor del juego
ocultar = false; // que no se vea la GUI
ciudadanos = 0; // cuantos personajes se tienen
estado = m_bonusiando; // momentos por los que pasa el juego en su ciclo
ciclo = 0; // reloj que maneja los ciclos del juego
seleccion = m_nada; // que edificio va a poner
mensaje = m_nada; // que mensaje enviaria a otro jugador
valormens = 1; // dato que se envia junto al mensaje
historia = 0; // numero de ciclos transcurridos

// variables de personajes
velcamina = 32 / m_segundo; // velocidad base de caminado
maxvida = 12; // puntos de impacto base
minataque = 1; // ataque basico
visual = 192; // campo visual base
solporcu = 4; // ciudadanos por edificio

// TCP web
m_puerto = 3456; // lo escogi porque me revento la gana
enmira = ""; // guarda al ultimo jugador seleccionado
master = false; // true si es el servidor
socket = -1; // coneccion creada para juegar chevere
conectados[0] = -1; // listado de sockets para sevidor
apodos[0] = ""; // listado de nombres de jugadores
usuarios = 0; // cuantos conectados hay
botar = irandom(100); // numero para eliminar nombre repetido

// visualizables en GUI
notificacion = "";
espionaje = "";

// precios edificios
valedif[0] = 0;
valedif[m_casa] = 2;
valedif[m_industria] = 3;
valedif[m_cuartel] = 4;
valedif[m_templo] = 5;

// precios envios
valenvi[0] = 0;
valenvi[m_regalo] = 1;
valenvi[m_robo] = 0.1;
valenvi[m_asedio] = 7;
valenvi[m_invasion] = 0.5;
valenvi[m_defensas] = 0.4;
valenvi[m_espias] = 3;
valenvi[m_persuacion] = 0.7;
valenvi[m_turistas] = 0.2;

s_abrir();

// mensajes asincronos por teclado
laip = -1; // luego pedira ip asincrono, si vacio entonces servidor
msg = get_string_async("Digite su Nombre", nombre);

repeat 11
{
    instance_create(room_width * 0.5, room_height * 0.5, pajaro);
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="6">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>network_destroy(socket);
game_restart();
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="5">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>if botar != -1
{
    network_destroy(socket);
    game_restart();
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="4">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// enviar apodos a todos
if master
{
    var txt;
    for (var u = 0; u &lt; usuarios; u++)
    {
        txt = "";
        for (var i = 0; i &lt; usuarios; i++)
        {
            if i &lt; m_lista
            {
                if apodos[i] == apodos[u]
                {
                    txt += "." + nombre;
                }
                else
                {
                    txt += "." + apodos[i];
                }
            }
        }
        s_envio(m_apodos, 0, txt, apodos[u]);
    }
}
alarm[4] = room_speed * 15;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="3">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// estadistic show
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="2">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// animales y matar enfermos
// crear animales salvajes al azar
if historia &gt; 9
{
    if random(1) &lt; 0.1
    {
        var dirr = irandom(359);
        var aux;
        repeat choose(1, 1, 1, 1, 1, 1, 1, 2, 2, 2, 2, 4, 4, 8)
        {
            aux = instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_p);
            aux.tipo = m_animal;
        }
        audio_play_sound(a_animales, 11, false);
    }
}

// matar sobrepoblacion enferma
var n = 0;
with personaje_a
{
    if tipo == m_defensa or tipo == m_turista
    {
        n++;
    }
}
n = solporcu * (instance_number(bloque) + 1) - n;
if n &lt; 0
{
    n = abs(n);
    with personaje_a
    {
        if (tipo == m_defensa or tipo == m_turista) and
            n &gt; 0 and random(1) &lt; 0.25
        {
            n--;
            var mt = instance_create(x, y, muerto);
            mt.tipo = tipo;
            instance_destroy();
        }
    }
}

alarm[2] = room_speed * (60 + irandom(60));
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="1">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// notificacion
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="2" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>/// contar poblacion
ciudadanos = instance_number(aliado);
if master
{
    s_envio(m_activo, 0, "?", "?");
}
alarm[0] = room_speed * 5;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// depurar segun estado del juego
switch estado
{
    case m_contando:
        ciclo = min(maxciclo, ciclo + delta_time);
        if ciclo == maxciclo
        {
            ciclo = 0;
            estado = m_bonusiando;
            audio_play_sound(a_turno, 100, false);
            historia++;
        }
    break;
    
    case m_bonusiando:
        monedas += 1 + instance_number(industria);
        var n = 0;
        with personaje_a
        {
            if tipo == m_ciudadano
            {
                n++;
            }
        }
        n = solporcu * 2 * (instance_number(casa) + 1) - n;
        with casa
        {
            if n &gt; 0
            {
                instance_create(x - 40 + irandom(80), y - 20 + irandom(40), personaje_a);
                n--;
            }
        }
        if n &gt; 0
        {
            var aux;
            if instance_exists(casa)
            {
                aux = instance_find(casa, irandom(instance_number(casa) - 1));
            }
            else if instance_exists(bloque)
            {
                aux = instance_find(bloque, irandom(instance_number(bloque) - 1));
            }
            else
            {
                aux = sombraedificios;
            }
            instance_create(aux.x - 40 + irandom(80), aux.y - 20 + irandom(40), personaje_a);
        }
        estado = m_construyendo;
    break;
    
    case m_construyendo:
        if keyboard_check_pressed(ord('S')) or mouse_wheel_up()
        {
            seleccion++;
            if seleccion &gt; m_templo { seleccion = m_nada; }
        }
        else if mouse_wheel_down()
        {
            seleccion --;
            if seleccion &lt; m_nada { seleccion = m_templo; }
        }
        else if keyboard_check_pressed(vk_space)
        {
            estado = m_enviando;
        }
        else if mouse_check_button_pressed(mb_left)
        {
            if seleccion == m_nada
            {
                estado = m_enviando;
            }
            else if mouse_x &gt; room_width * 0.1 and mouse_x &lt; room_width * 0.9 and
                mouse_y &gt; room_height * 0.1 and mouse_y &lt; room_height * 0.9
            {
                var coli = false;
                with sombraedificios
                {
                    if place_meeting(mouse_x, mouse_y, bloque)
                    {
                        coli = true;
                    }
                }
                if monedas &gt;= valedif[seleccion] and !coli
                {
                    switch seleccion
                    {
                        case m_casa: instance_create(mouse_x, mouse_y, casa); break;
                        case m_cuartel: instance_create(mouse_x, mouse_y, cuartel); break;
                        case m_industria: instance_create(mouse_x, mouse_y, industria); break;
                        case m_templo: instance_create(mouse_x, mouse_y, templo); break;
                    }
                    monedas -= valedif[seleccion];
                    control.estado = m_enviando;
                    audio_play_sound(a_poner, 30, false);
                }
            }
        }
    break;
    
    case m_enviando:
        if mouse_check_button_pressed(mb_left)
        {
            var p;
            for (var u = 0; u &lt; usuarios; u++)
            {
                p = room_height - (82 + u * 20);
                if mouse_x &gt; room_width - 170 and mouse_x &lt; room_width and
                    mouse_y &gt; p - 9 and mouse_y &lt; p + 9
                {
                    enmira = apodos[u];
                    break;
                }
            }
        }
        else if mouse_wheel_up()
        {
            mensaje++;
            if mensaje &gt; m_espias { mensaje = m_nada; }
        }
        else if mouse_wheel_down()
        {
            mensaje--;
            if mensaje &lt; m_nada { mensaje = m_espias; }
        }
        else if keyboard_check_pressed(vk_anykey)
        {
            switch keyboard_key
            {
                case ord('M'):
                    mensaje++;
                    if mensaje &gt; m_espias { mensaje = m_nada; }
                break;
                
                case ord('C'):
                    valormens *= 2;
                    if valormens &gt; 16 { valormens = 1; }
                break;
                
                case vk_space:
                    estado = m_recibiendo;
                break;
                
                case vk_enter:
                    // ver si habra envio
                    if mensaje == m_nada
                    {
                        estado = m_recibiendo;
                    }
                    else
                    {
                        // ver si enmira esta en la lista
                        var esta = false;
                        for (var u = 0; u &lt; usuarios; u++)
                        {
                            if apodos[u] == enmira
                            {
                                esta = true;
                                break;
                            }
                        }
                        if !esta
                        {
                            enmira = "";
                        }
                        // ver si habra envio
                        if enmira != ""
                        {
                            // calcular cobro
                            var elcosto, lacosta;
                            if mensaje != m_asedio and mensaje != m_espias
                            {
                                elcosto = valenvi[mensaje] * valormens;
                                lacosta = valormens;
                            }
                            else
                            {
                                elcosto = valenvi[mensaje];
                                lacosta = 1;
                            }
                            // ver si alcanza el cobro
                            if monedas &gt;= elcosto and ciudadanos &gt;= lacosta
                            {
                                // enviar el mensaje por TCP
                                s_envio(mensaje, valormens, nombre, enmira);
                                audio_play_sound(a_enviando, 60, false);
                                // cobrar y desaparecer a los tipos
                                monedas -= elcosto;
                                var num, ese;
                                repeat lacosta
                                {
                                    num = instance_number(personaje_a);
                                    if num &gt; 0
                                    {
                                        ese = noone;
                                        with personaje_a
                                        {
                                            if tipo == m_ciudadano or tipo == m_turista or
                                                tipo == m_obrero or tipo == m_brujo
                                            {
                                                ese = id;
                                                break;
                                            }
                                        }
                                        if ese == noone
                                        {
                                            ese = instance_find(personaje_a, irandom(num - 1));
                                        }
                                        with ese { instance_destroy(); }
                                    }
                                }
                                estado = m_recibiendo;
                            }
                        }
                    }
                break;
            }
        }
    break;
    
    case m_recibiendo:
        s_guardar();
        estado = m_contando;
    break;
}

// acciones generales
if keyboard_check_pressed(vk_anykey)
{
    switch keyboard_key
    {
        case vk_escape: if keyboard_check(vk_shift) { game_end(); } break;
        case vk_backspace:
            if keyboard_check(vk_shift)
            {
                with bloque { instance_destroy(); }
                with movil { instance_destroy(); }
                monedas = 0;
                historia = 0;
                estado = m_bonusiando;
                ciclo = 0;
                if file_exists("save.ini") { file_delete("save.ini"); }
                network_destroy(socket);
                game_restart();
            }
            break;
        case vk_f4: window_set_fullscreen(!window_get_fullscreen()); break;
        case vk_tab: ocultar = !ocultar; break;
    }
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="68">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// recibe mensajes enviados por TCP
var motivo = ds_map_find_value(async_load, "type");
switch motivo
{
    case network_type_connect:
        var cliente = ds_map_find_value(async_load, "socket");
        usuarios++;
        conectados[usuarios - 1] = cliente;
        apodos[usuarios - 1] = "";
    break;
    
    case network_type_disconnect:
        var cliente = ds_map_find_value(async_load, "socket");
        for (var n = 0; n &lt; usuarios; n++)
        {
            if conectados[n] == cliente
            {
                for (var d = n + 1; d &lt; usuarios; d++)
                {
                    conectados[d - 1] = conectados[d];
                    apodos[d - 1] = apodos[d];
                }
                break;
            }
        }
        usuarios--;
    break;
    
    case network_type_data:
        var buf = ds_map_find_value(async_load, "buffer");
        buffer_seek(buf, buffer_seek_start, 0);
        var msj = buffer_read(buf, buffer_u8);
        var valmsj = buffer_read(buf, buffer_u8);
        var emisor = buffer_read(buf, buffer_string);
        var recept = buffer_read(buf, buffer_string);
        buffer_delete(buf);
        
        if master and msj == m_nombre
        {
            // agregar nombre nuevo a la lista
            var libre = true;
            for (var n = 0; n &lt; usuarios; n++)
            {
                if apodos[n] == emisor
                {
                    libre = false;
                    break;
                }
            }
            if libre
            {
                var cliente = ds_map_find_value(async_load, "id");
                for (var n = 0; n &lt; usuarios; n++)
                {
                    if conectados[n] == cliente
                    {
                        apodos[n] = emisor;
                        s_envio(m_aceptado, 0, "?", emisor);
                        break;
                    }
                }
            }
            else
            {
                s_envio(m_expulsado, valmsj, "?", emisor);
            }
        }
        else if !master and msj == m_expulsado
        {
            // el usuario fue botado por nombre repetido
            if recept == nombre and botar == valmsj
            {
                network_destroy(socket);
                game_restart();
            }
        }
        else if !master and msj == m_activo
        {
            alarm[6] = room_speed * 10;
        }
        else if recept != nombre
        {
            // rebotar mensajes a otros usuarios
            if master
            {
                s_envio(msj, valmsj, emisor, recept);
            }
        }
        else
        {
            // depurar los tipos de mensajes
            var lacosa = "";
            var aux;
            var dirr = irandom(359);
            switch msj
            {
                case m_robo:
                    lacosa = " Ladrones";
                    repeat valmsj
                    {
                        aux = instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                            room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_n);
                        aux.tipo = m_ladron;
                        aux.dequien = emisor;
                    }
                    audio_play_sound(a_robo, 50, false);
                break;
                
                case m_regalo:
                    lacosa = " Monedas";
                    repeat valmsj
                    {
                        aux = instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                            room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_n);
                        aux.tipo = m_donante;
                        aux.dequien = emisor;
                    }
                    audio_play_sound(a_regalo, 50, false);
                break;
                
                case m_invasion:
                    lacosa = " Atacantes";
                    repeat valmsj
                    {
                        aux = instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                            room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_p);
                        aux.tipo = m_enemigo;
                        aux.dequien = emisor;
                    }
                    audio_play_sound(a_invasion, 50, false);
                break;
                
                case m_persuacion:
                    lacosa = " Raros";
                    repeat valmsj
                    {
                        aux = instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                            room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_n);
                        aux.tipo = m_raro;
                        aux.dequien = emisor;
                    }
                    audio_play_sound(a_persuacion, 50, false);
                break;
                
                case m_defensas:
                    lacosa = " Defensas";
                    repeat valmsj
                    {
                        aux = instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                            room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_a);
                        aux.tipo = m_defensa;
                        aux.dequien = emisor;
                    }
                    audio_play_sound(a_defensas, 50, false);
                break;
                
                case m_turistas:
                    lacosa = " Ciudadanos";
                    repeat valmsj
                    {
                        aux = instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                            room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_a);
                        aux.tipo = m_turista;
                        aux.dequien = emisor;
                    }
                    audio_play_sound(a_turistas, 50, false);
                break;
                
                case m_asedio:
                    lacosa = " Explosivo";
                    aux = instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                        room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_p);
                    aux.tipo = m_bomba;
                    aux.dequien = emisor;
                    valmsj = 1;
                    audio_play_sound(a_asedio, 50, false);
                break;
                
                case m_espias:
                    lacosa = " Espia";
                    aux = instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                        room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_a);
                    aux.tipo = m_turista;
                    aux.dequien = emisor;
                    valmsj = 1;
                    audio_play_sound(a_espias, 50, false);
                    // envia chisme datos
                    var estad = nombre + ": Edi: " + string(instance_number(bloque)) +
                        ", Pob: " + string(ciudadanos) + ", Din: $" +
                        string(round(monedas * 10));
                    s_envio(m_estadisticas, 0, estad, emisor);
                break;
                
                case m_estadisticas:
                    alarm[3] = room_speed * 60;
                    espionaje = emisor;
                break;
                
                case m_volvio:
                    monedas += valmsj;
                    instance_create(room_width * 0.5 + lengthdir_x(room_width, dirr),
                        room_height * 0.5 + lengthdir_y(room_height, dirr), personaje_a);
                break;
                
                case m_apodos:
                    if !master
                    {
                        var c;
                        usuarios = string_count(".", emisor);
                        for (var u = 0; u &lt; usuarios; u++)
                        {
                            emisor = string_replace(emisor, ".", "");
                            apodos[u] = "";
                            while emisor != ""
                            {
                                c = string_char_at(emisor, 1);
                                if c != "."
                                {
                                    apodos[u] += c;
                                    emisor = string_replace(emisor, c, "");
                                }
                                else { break; }
                            }
                        }
                    }
                break;
                
                case m_aceptado:
                    botar = -1;
                break;
            }
            // dibuja las notificaciones
            if lacosa != ""
            {
                alarm[1] = room_speed * 15;
                if lacosa == " Monedas"
                {
                   notificacion = emisor + " envia $" + string(round(valmsj * 10)) + lacosa; 
                }
                else
                {
                    notificacion = emisor + " envia " + string(valmsj) + lacosa;
                }
            }
        }
    break;
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="63">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>var la_id = ds_map_find_value(async_load, "id")
if la_id == msg
{
    if ds_map_find_value(async_load, "status")
    {
        nombre = s_pulename(ds_map_find_value(async_load, "result"));
    }
    if nombre == ""
    {
        nombre = string(irandom(9)) + string(irandom(9)) +
            string(irandom(9)) + string(irandom(9));
    }
    if remote_ip[0] == "0.0.0.0"
    {
        laip = get_string_async("Digite Ip Del Servidor, Ejemplo: 127.0.0.1" +
            ", Para Crear Servidor Deje Vacio", "");
    }
    else
    {
        laip = get_string_async("Digite Ip Del Servidor, Ejemplo: 127.0.0.1" +
            ", Para Crear Servidor Deje Vacio", remote_ip[0]);
    }
    msg = -1;
}
else if la_id == laip
{
    if ds_map_find_value(async_load, "status")
    {
        var txt = ds_map_find_value(async_load, "result");
        if txt == ""
        {
            // crear servidor
            socket = network_create_server(network_socket_tcp, m_puerto, m_lista);
            if socket &lt; 0
            {
                game_restart();
            }
            master = true;
        }
        else
        {
            // crear cliente
            socket = network_create_socket(network_socket_tcp);
            if socket &lt; 0
            {
                game_restart();
            }
            var ok = network_connect(socket, txt, m_puerto);
            if ok &lt; 0
            {
                network_destroy(socket);
                game_restart();
            }
            s_envio(m_nombre, botar, nombre, "?");
            alarm[5] = room_speed * 5;
            alarm[6] = room_speed * 10;
            master = false;
        }
    }
    laip = -1;
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="7" enumb="3">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>s_guardar();
network_destroy(socket);
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>if !ocultar
{
    // rectangulos blancos de fondo
    draw_set_alpha(0.5);
    draw_rectangle_colour(0, 0, 290, 350, c_white, c_white, c_white, c_white, false);
    draw_rectangle_colour(room_width - 280, 0, room_width, 120,
        c_white, c_white, c_white, c_white, false);
    draw_rectangle_colour(room_width - 600, room_height - 60, room_width, room_height,
        c_white, c_white, c_white, c_white, false);
    if estado == m_enviando
    {
        draw_rectangle_colour(room_width - 212, room_height - (60 + (usuarios + 1) * 20.3),
            room_width, room_height - 60, c_white, c_white, c_white, c_white, false);
    }
    draw_rectangle_colour(0, room_height - 43, 180, room_height,
        c_white, c_white, c_white, c_white, false);
    draw_set_alpha(1);
    
    // textos superior izquierdo
    var txt = "Nombre: " + nombre +
        "#Historia: " + string(historia) +
        "#MaxCiclo: " + string(round(maxciclo / m_segundo)) + "s";
    if estado == m_contando
    {
        txt += "#Ciclo: " + string(floor((ciclo / maxciclo) * 100)) + "%";
    }
    else
    {
        txt += "#Ciclo: Tu Turno!!!";
    }
    txt += "#...#Monedas: $" + string(round(monedas * 10)) +
        "#Ciudadanos: " + string(ciudadanos);
    if master
    {
        txt += "#Web: Server " + string(usuarios) + "#...";
    }
    else
    {
        txt += "#Web: Client " + string(usuarios) + "#...";
    }
    if estado == m_construyendo
    {
        txt += "#(S)Seleccion: ";
        switch seleccion
        {
            case m_casa: txt += "Vivienda"; break;
            case m_cuartel: txt += "Cuartel"; break;
            case m_industria: txt += "Industria"; break;
            case m_templo: txt += "Templo"; break;
            default: txt += "..."; break;
        }
        txt += "#Precio: $" + string(round(valedif[seleccion] * 10)) +
            "#(Clic-Izq): Construir#(Espacio): Continuar";
    }
    else if estado == m_enviando
    {
        txt += "#(M)Mensaje: ";
        switch mensaje
        {
            case m_regalo: txt += "Regalo"; break;
            case m_robo: txt += "Robo"; break;
            case m_turistas: txt += "Turistas"; break;
            case m_persuacion: txt += "Persuacion"; break;
            case m_defensas: txt += "Defensas"; break;
            case m_invasion: txt += "Invasion"; break;
            case m_asedio: txt += "Asedio"; break;
            case m_espias: txt += "Espias"; break;
            default: txt += "..."; break;
        }
        if mensaje != m_asedio and mensaje != m_espias
        {
            txt += "#Costo: $" + string(round(valenvi[mensaje] * valormens * 10));
        }
        else
        {
            txt += "#Costo: $" + string(round(valenvi[mensaje] * 10));
        }
        txt += "#(C)Cantidad: " + string(valormens) +
            "#Quien: " + enmira +
            "#(Espacio): Continuar#(Enter): Enviar";
    }
    draw_set_valign(fa_top);
    draw_set_halign(fa_left);
    draw_text_transformed_colour(0, 0, txt, 1.5, 1.5, 0,
        c_black, c_black, c_black, c_black, 1);
    
    // textos superior derecho
    txt = "Joda Monigotica#Omarsaurio 2019#Salir :(Escape+Mayus)" +
        "#Reset :(Borrar+Mayus)#Full-Screen :(F4)"
    draw_set_valign(fa_top);
    draw_set_halign(fa_right);
    draw_text_transformed_colour(room_width, 0, txt, 1.5, 1.5, 0,
        c_black, c_black, c_black, c_black, 1);
    
    // textos inferior derecho, notificaciones
    if alarm[1] != -1
    {
        txt = notificacion;
        if alarm[3] != -1
        {
            txt += "#" + espionaje;
        }
        else
        {
            txt += "#...";
        }
    }
    else if alarm[3] != -1
    {
        txt = "...#" + espionaje;
    }
    else
    {
        txt = "...#...";
    }
    draw_set_valign(fa_bottom);
    draw_set_halign(fa_right);
    draw_text_transformed_colour(room_width, room_height, txt, 1.5, 1.5, 0,
        c_black, c_black, c_black, c_black, 1);
    
    // textos inferior derecho, listado de usuarios
    if estado == m_enviando
    {
        draw_set_valign(fa_middle);
        draw_set_halign(fa_right);
        var p;
        for (var u = 0; u &lt; usuarios; u++)
        {
            p = room_height - (82 + u * 20);
            if mouse_x &gt; room_width - 170 and mouse_x &lt; room_width and
                mouse_y &gt; p - 9 and mouse_y &lt; p + 9
            {
                draw_text_transformed_colour(room_width, p,
                    apodos[u], 1.5, 1.5, 0,
                    c_red, c_red, c_red, c_red, 1);
            }
            else
            {
                draw_text_transformed_colour(room_width, p,
                    apodos[u], 1.5, 1.5, 0,
                    c_black, c_black, c_black, c_black, 1);
            }
        }
    }
    
    // textos inferior izquierdo
    draw_set_valign(fa_bottom);
    draw_set_halign(fa_left);
    draw_text_transformed_colour(0, room_height, "(Tab):Ocultar", 1.5, 1.5, 0,
        c_black, c_black, c_black, c_black, 1);
}
else
{
    draw_set_alpha(0.5);
    draw_rectangle_colour(0, room_height - 43, 180, room_height,
        c_white, c_white, c_white, c_white, false);
    draw_set_alpha(1);
    
    // textos inferior izquierdo
    draw_set_valign(fa_bottom);
    draw_set_halign(fa_left);
    draw_text_transformed_colour(0, room_height, "(Tab):Mostrar", 1.5, 1.5, 0,
        c_black, c_black, c_black, c_black, 1);
}

// dibujar puntero
draw_sprite_ext(d_mouse, 0, mouse_x, mouse_y, 1.5, 1.5, 0, c_white, 1);
if false
{
    draw_set_valign(fa_bottom);
    draw_set_halign(fa_right);
    draw_text_transformed_colour(mouse_x, mouse_y,
        string(mouse_x) + "," + string(mouse_y), 1, 1, 0,
        c_black, c_black, c_black, c_black, 1);
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
